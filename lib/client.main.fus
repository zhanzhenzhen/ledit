fus 0.7.0

import "babel-polyfill"
client: import "site/client"
ui: client.ui
web: npmMate.web

global.editor: null
global.fusApi: require("futurescript/es5-target/locked-api")

currentDirectory: null

extname: filename ->
    pos: filename.lastIndexOf(".")
    if pos = -1 or pos = 0
        ""
    else
        filename.substr(pos)

newScene: ui.Stack(
    {
        horizontal: true
    },
    ui.Stack(
        {
            width: 0.2
            padding: "1rem"
            childSpacing: "1rem"
        },
        ui.Text("/") as dirText,
        ui.Text("(no file)") as fileText,
        ui.Button("Up").setWidth(1) as upButton,
        ui.Stack{childSpacing: "0.5rem", overflow: "scroll"} as filesStack
    ) as leftStack,
    ui.Element(
        {
            width: 0.8
            height: 1
            selectable: true
            pointer: "auto"
        },
        "
            <div id="editor" style="width: 100%; height: 100%;" />
        "
    ) as editorElement
)

upButton.raw.style.borderColor: "rgb(240,240,240)"
upButton.onClick <>
    if currentDirectory ≠ "/"
        pos: currentDirectory.lastIndexOf("/")
        showFiles(pos = 0 ? "/" | currentDirectory.substr(0, pos))

ui.root
.setBackgroundPaint("rgb(39,40,34)")
.setPaint("rgb(240,240,240)")
.add(newScene)

showFiles: path ->
    currentDirectory: path
    dirText.setText path
    filesStack.empty()
    web.jsonPost("/svc/files", path)'wait.body.forEach file ->
        text: ui.Text(file.name)
        text.onClick <>
            if file.isDirectory
                showFiles(file.path)
            else
                fileText.setText file.name
                mode: match extname(file.name)
                    ".fus"    ? "fus"
                    ".js"     ? "javascript"
                    ".coffee" ? "coffee"
                    ".cpp",
                    ".c",
                    ".cc",
                    ".h",
                    ".hh",
                    ".hpp"    ? "c_cpp"
                    ".css"    ? "css"
                    ".html",
                    ".htm",
                    ".xhtml"  ? "html"
                    ".json"   ? "json"
                    ".svg"    ? "svg"
                    ".xml"    ? "xml"
                    ".md"     ? "markdown"
                    |           "plain_text"
                editor.getSession().setMode("ace/mode/\(mode)")
                editor.setValue web.jsonPost("/svc/file/read", file.path)'wait.body
                if not client.browser.isTouch
                    editor.focus()
                editor.navigateFileStart()
        text.setPointer "link"
        filesStack.add text
showFiles "/"

do --
    global.editor: ace.edit("editor")
    editor.commands.addCommands[
        {
            bindKey: "Alt-="
            exec: -- editor.insert("≠")
        }
        {
            bindKey: "Alt-,"
            exec: -- editor.insert("≤")
        }
        {
            bindKey: "Alt-."
            exec: -- editor.insert("≥")
        }
    ]
    editor.setShowPrintMargin(false)
    editor.setTheme("ace/theme/monokai_adjusted")
    #editor.getSession().setMode("ace/mode/fus")
    editor.getSession().setNewLineMode("unix")
    #editor.getSession().setUseWrapMode(true)
    editor.getSession().on("change", e ->
        if editor.getSession().getMode().$id = "ace/mode/fus"
            editor.getSession().resetCaches()
    )
    editor.focus()
    editor.navigateFileEnd()
    width: ""
    height: ""
    element: document.getElementById("editor")
    setInterval(--
        style: getComputedStyle(element)
        if style.width ≠ width or style.height ≠ height
            editor.resize()
            width: style.width
            height: style.height
    , 500)
