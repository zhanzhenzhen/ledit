fus 0.7.0

import "babel-polyfill"
client: import "site/client"
ui: client.ui
web: npmMate.web

global.editor: null
global.fusApi: require("futurescript/es5-target/locked-api")

newScene: ui.Stack(
    {
        horizontal: true
    },
    ui.Stack(
        {
            width: 0.2
        },
        ui.Text("/").setHeightInRem(2) as dirText,
        ui.Text("").setHeightInRem(2) as fileText,
        ui.Button("Up").setWidth(1) as upButton,
        ui.Stack() as filesStack
    ) as leftStack,
    ui.Element(
        {
            width: 0.8
            height: 1
            selectable: true
            pointer: "auto"
        },
        "
            <div id="editor" style="width: 100%; height: 100%;" />
        "
    ) as editorElement
)

ui.root
.setBackgroundPaint("rgb(39,40,34)")
.setPaint("rgb(240,240,240)")
.add(newScene)

showFiles: path ->
    filesStack.empty()
    web.jsonPost("/svc/files", path)'wait.body.forEach file ->
        text: ui.Text(file.name)
        text.onClick <>
            if file.isDirectory
                showFiles(file.path)
            else
                editor.setValue web.jsonPost("/svc/file/read", file.path)'wait.body
                if not client.browser.isTouch
                    editor.focus()
                editor.navigateFileStart()
        filesStack.add text
showFiles "/"

do --
    global.editor: ace.edit("editor")
    editor.commands.addCommands[
        {
            bindKey: "Alt-="
            exec: -- editor.insert("≠")
        }
        {
            bindKey: "Alt-,"
            exec: -- editor.insert("≤")
        }
        {
            bindKey: "Alt-."
            exec: -- editor.insert("≥")
        }
    ]
    editor.setShowPrintMargin(false)
    editor.setTheme("ace/theme/monokai_adjusted")
    editor.getSession().setMode("ace/mode/fus")
    editor.getSession().setNewLineMode("unix")
    #editor.getSession().setUseWrapMode(true)
    editor.getSession().on("change", e ->
        editor.getSession().resetCaches()
    )
    editor.focus()
    editor.navigateFileEnd()
    width: ""
    height: ""
    element: document.getElementById("editor")
    setInterval(--
        style: getComputedStyle(element)
        if style.width ≠ width or style.height ≠ height
            editor.resize()
            width: style.width
            height: style.height
    , 500)
